const VAR_OBJ_GFX_TEA_SHOP = VAR_OBJ_GFX_ID_0

mapscripts Rogue_Area_DayCare_MapScripts 
{
    MAP_SCRIPT_ON_LOAD: Rogue_Area_DayCare_OnMapLoad
}

script Rogue_Area_DayCare_OnMapLoad
{
    random(2)
    if(var(VAR_RESULT) == 0)
    {
        setvar(VAR_OBJ_GFX_TEA_SHOP, OBJ_EVENT_GFX_MISC_YOUNG_COUPLE_M)
    }
    else
    {
        setvar(VAR_OBJ_GFX_TEA_SHOP, OBJ_EVENT_GFX_MISC_YOUNG_COUPLE_F)
    }
}

script Rogue_Area_DayCare_BlendPokeblockNovice
{
    setflag(FLAG_HIDE_LILYCOVE_CONTEST_HALL_BLEND_MASTER)
    
    lock

    // NOVICE_BERRY_COUNT
    msgbox(format("Hello!\nI'm a {POKEBLOCK} Blending Novice.\pI can help you produce up to 10 {POKEBLOCK} per 20 berries.\pWould you like to blend some {POKEBLOCK} with me?"), MSGBOX_YESNO)

    if(var(VAR_RESULT) == YES)
    {
        specialvar(VAR_RESULT, PlayerHasBerries)

        if(var(VAR_RESULT) == YES)
        {
            // NOVICE_BERRY_COUNT
            setvar(VAR_0x800A, 20) // num berries

            setvar(VAR_RESULT, YES)
            while(var(VAR_RESULT) == YES)
            {
                call(Rogue_Area_DayCare_BlendPokeblockMasterLoop)
            }
        }
        else
        {
            msgbox(format("Oh.\pIt doesn't look like you have any berries to blend."))
        }
    }
    else
    {
        msgbox(format("I see.\nIf you change your mind feel free to speak with me again."))
    }

    release
}

script Rogue_Area_DayCare_BlendPokeblockNoviceLoop
{
    msgbox(format("Which berry would you like to blend?"))

    fadescreen(FADE_TO_BLACK)
    special(Bag_ChooseBerry)
    waitstate

    if(var(VAR_ITEM_ID) != 0)
    {
        special(Rogue_IsValidPokeblockBerry)
        if(var(VAR_RESULT) == YES)
        {
            // NOVICE_BERRY_COUNT
            checkitem(VAR_ITEM_ID, 20)

            if(var(VAR_RESULT) == YES)
            {
                msgbox(format("OK.\pLet's blend!"))

                waitse
                setvar(VAR_0x8004, 1) // NUM_OPPONENTS
                fadescreen(FADE_TO_BLACK)
                special(DoBerryBlending)
                waitstate

                call(Rogue_Common_DisplayPopups)

                msgbox(format("Would you like to blend more berries?"), MSGBOX_YESNO)
            }
            else
            {
                // NOVICE_BERRY_COUNT
                msgbox(format("You don't have enough berries.\pYou need 20 berries to blend with me."))
            }
        }
        else
        {
            msgbox(format("I'm afraid that berry won't produce any {POKEBLOCK}.\pYou can use the Check Tag option in your Bag to see which {POKEBLOCK} a berry will produce when blended."))
        }
    }
    else
    {
        setvar(VAR_RESULT, NO)
    }
}

script Rogue_Area_DayCare_BlendPokeblockMaster
{
    clearflag(FLAG_HIDE_LILYCOVE_CONTEST_HALL_BLEND_MASTER)
    
    lock

    // MASTER_BERRY_COUNT
    msgbox(format("Hello!\nI'm a {POKEBLOCK} Blending Master.\pI can help you produce up to 30 {POKEBLOCK} per 70 berries.\pWould you like to blend some {POKEBLOCK} with me?"), MSGBOX_YESNO)

    if(var(VAR_RESULT) == YES)
    {
        specialvar(VAR_RESULT, PlayerHasBerries)

        if(var(VAR_RESULT) == YES)
        {
            // MASTER_BERRY_COUNT
            setvar(VAR_0x800A, 70) // num berries

            setvar(VAR_RESULT, YES)
            while(var(VAR_RESULT) == YES)
            {
                call(Rogue_Area_DayCare_BlendPokeblockMasterLoop)
            }
        }
        else
        {
            msgbox(format("Oh.\pIt doesn't look like you have any berries to blend."))
        }
    }
    else
    {
        msgbox(format("I see.\nIf you change your mind feel free to speak with me again."))
    }

    release
}

script Rogue_Area_DayCare_BlendPokeblockMasterLoop
{
    msgbox(format("Which berry would you like to blend?"))

    fadescreen(FADE_TO_BLACK)
    special(Bag_ChooseBerry)
    waitstate

    if(var(VAR_ITEM_ID) != 0)
    {
        special(Rogue_IsValidPokeblockBerry)
        if(var(VAR_RESULT) == YES)
        {
            // MASTER_BERRY_COUNT
            checkitem(VAR_ITEM_ID, 70)

            if(var(VAR_RESULT) == YES)
            {
                msgbox(format("OK.\pLet's blend!"))

                waitse
                setvar(VAR_0x8004, 1) // NUM_OPPONENTS
                fadescreen(FADE_TO_BLACK)
                special(DoBerryBlending)
                waitstate

                call(Rogue_Common_DisplayPopups)

                msgbox(format("Would you like to blend more berries?"), MSGBOX_YESNO)
            }
            else
            {
                msgbox(format("You don't have enough berries.\pYou need 70 berries to blend with me."))
            }
        }
        else
        {
            msgbox(format("I'm afraid that berry won't produce any {POKEBLOCK}.\pYou can use the Check Tag option in your Bag to see which {POKEBLOCK} a berry will produce when blended."))
        }
    }
    else
    {
        setvar(VAR_RESULT, NO)
    }
}

script Rogue_Area_DayCare_TalkBerryBlender
{
    lock
    msgbox(format("They're intensely focusing on using the Berry Blending Machine."))
    release
}

script Rogue_Area_DayCare_SwapMonGender
{
    lock
    faceplayer

    setvar(VAR_0x8004, 0)
    special(BufferMonNickname)
    msgbox(format("Well hi there!\pI just brewed a fresh batch of Tea.\nSometimes Pokémon who drink it can change gender!\pDid you want me to share some with {STR_VAR_1}?"), MSGBOX_YESNO)

    if(var(VAR_RESULT) == YES)
    {
        fadescreen(FADE_TO_BLACK)

        playse(SE_USE_ITEM)
        waitse
        special(Rogue_SwapMonGender)

        fadescreen(FADE_FROM_BLACK)
    }
    else
    {
        msgbox(format("Well, I have plenty of Herbal Remedy Tea to go around, if you change your mind!"))
    }

    release
}

script Rogue_Area_DayCare_TalkEggBreeder
{
    lock
    faceplayer

    msgbox(format("Hello there, {PLAYER}!"))

    if(flag(FLAG_ROGUE_DAYCARE_EGG_READY))
    {
        bufferspeciesname(STR_VAR_1, VAR_ROGUE_DAYCARE_EGG)
        msgbox(format("I managed to find that {STR_VAR_1} egg you were looking for!"))

        giveegg(VAR_ROGUE_DAYCARE_EGG)

        clearflag(FLAG_ROGUE_DAYCARE_EGG_READY)
        setvar(VAR_ROGUE_DAYCARE_EGG, SPECIES_NONE)
    }
    else
    {
        if(var(VAR_ROGUE_DAYCARE_EGG) != SPECIES_NONE)
        {
            bufferspeciesname(STR_VAR_1, VAR_ROGUE_DAYCARE_EGG)
            msgbox(format("I've not managed to locate a {STR_VAR_1} egg quite yet.\pWould you like for me to look for a different type of Pokémon instead?"), MSGBOX_YESNO)
        }
        else
        {
            msgbox(format("If you'd like I can attempt to acquire a Pokémon for you whilst, you're out Adventuring!\pWhich Pokémon would you like and egg for?"))
            setvar(VAR_RESULT, YES)
        }

        if(var(VAR_RESULT) == YES)
        {
            special(Special_SelectMonInPokedex)
            waitstate

            if(var(VAR_RESULT) != SPECIES_NONE)
            {
                special(Rogue_TransformIntoValidDaycareEgg)
                
                if(var(VAR_RESULT) == SPECIES_NONE)
                {
                    msgbox(format("I'm not sure I would be able to locate an Egg for that Pokémon, I'm afraid."))
                }
                else
                {
                    clearflag(FLAG_ROGUE_DAYCARE_EGG_READY)
                    copyvar(VAR_ROGUE_DAYCARE_EGG, VAR_RESULT)
                    bufferspeciesname(STR_VAR_1, VAR_ROGUE_DAYCARE_EGG)

                    msgbox(format("Alright!\nI'll keep my eyes out for a {STR_VAR_1} egg."))
                }
            }
        }
    }

    release
}

script Rogue_Area_DayCare_InteractWithPhoneBooth
{
    specialvar(VAR_RESULT, GetPlayerFacingDirection)
    if(var(VAR_RESULT) == DIR_NORTH)
    {
        lock
        msgbox(format("It's the Day Care's phone!"))

        if(!flag(FLAG_ROGUE_RUN_ACTIVE))
        {
            checkitem(ITEM_DAYCARE_PHONE)

            if(var(VAR_RESULT) == NO)
            {
                msgbox(format("Oh?\nThere's a small package with a note attached.\pIt reads…\p'Dear {PLAYER}, please accept this Pokégear.\pYou can use it to remotely make requests with the Day Care, whilst you're out on Adventures.\pBest of luck!\p - The Day Care Staff'"))
                giveitem(ITEM_DAYCARE_PHONE)
            }
        }

        release
    }
}